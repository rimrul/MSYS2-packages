# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=openssh
pkgver=9.1p1
pkgrel=0
pkgdesc='Free version of the SSH connectivity tools'
url='http://www.openssh.org/portable.html'
license=('custom:BSD')
arch=('i686' 'x86_64')
groups=('net-utils')
depends=('libfido2' 'heimdal' 'libedit' 'libcrypt' 'openssl')
makedepends=('libfido2-devel' 'libcbor-devel' 'heimdal-devel' 'libedit-devel' 'libcrypt-devel' 'openssl-devel')
source=("https://cloudflare.cdn.openbsd.org/pub/OpenBSD/OpenSSH/portable/${pkgname}-${pkgver}.tar.gz"{,.asc}
        0001-Forward-port-MSys2-patches.patch
        0002-openssh-work-around-Cygwin-declaring-setkey.patch
        0003-openssh-skip-privilege-separation-tests.patch
        0004-Allow-scp-to-copy-files-that-start-with-a-Windows-dr.patch)
sha256sums=('19f85009c7e3e23787f0236fbb1578392ab4d4bf9f8ec5fe6bc1cd7e8bfdd288'
            'SKIP'
            'a83d6e788bb2c6475660bec0435f36fe3cbc86438be373d86d1d573bb8bb2106'
            '3dc709506d85d92c989dc986d498525785fc84d66f25ebc7bec80ab1dccbc773'
            '7b0088ee3282a163bd2efe20b011c713f709d33dac50d3c04ab5500eff69c287'
            'd82f88c1bd64ea8ffcde735fb80460dd700bca19b039b52c229b5352df3989f7')
validpgpkeys=('59C2118ED206D927E667EBE3D3E5F56B6D920D30'
              '7168B983815A5EEF59A4ADFD2A3F414E736060BA')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-Forward-port-MSys2-patches.patch
  patch -p1 -i ${srcdir}/0002-openssh-work-around-Cygwin-declaring-setkey.patch
  patch -p1 -i ${srcdir}/0003-openssh-skip-privilege-separation-tests.patch
  patch -p1 -i ${srcdir}/0004-Allow-scp-to-copy-files-that-start-with-a-Windows-dr.patch
  autoreconf -fvi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  TEST_SSH_UTF8=no \
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh \
    --localstatedir=/var \
    --with-libedit \
    --with-kerberos5=/usr \
    --disable-strip \
    --with-security-key-builtin

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make tests ||
  grep $USER /etc/passwd | grep -q /bin/false
  # connect.sh fails when run with stupid login shell
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #ln -sf ssh.1.gz "${pkgdir}"/usr/share/man/man1/slogin.1.gz
  install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"

  install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
  install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
  install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

  #rm -f "${pkgdir}"/etc/ssh/sshd_config
  #rm -f "${pkgdir}"/usr/bin/sshd.exe
  #rm -f "${pkgdir}"/usr/sbin/sshd.exe
  #rm -f "${pkgdir}"/usr/lib/ssh/sftp-server.exe
}
